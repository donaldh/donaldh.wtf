---
title: "Spelling Bee"
date: 2019-11-03T17:39:31Z
tags:
  - perl6
  - raku
categories:
  - code
comments: true
---

A solver for the NYT daily *Spelling Bee*, written in Raku /(nee Perl 6)/.

<!--more-->

A spelling be is a hex grid of characters that are to be used to produce words.  The goal is to
get the highest score possible by finding words, with longer words giving better scores.

[[file:bee.png]]

These are the spelling bee rules:

- Each word must be at least 4 letters
- Each word must contain the centre letter
- Letters can be used more than once

This solver finds all the matching words from ~/usr/share/dict/words~.

#+BEGIN_SRC raku :results output
my @words = '/usr/share/dict/words'.IO.lines>>.lc;

my $centre = 't';
my @allowed = <m c i p l o t>;

for @words -> $word {
    my @chars = $word.comb;
    say $word if
        +@chars >= 4 &&             # Is at least 4 chars
        $centre (elem) @chars &&    # Contains the center char (element of set)
        @chars (<=) @allowed;       # All chars are allowed (is a subset)
}
#+END_SRC

This spelling bee produces the following words:

#+RESULTS:
#+begin_example
clipt
clit
cloit
cloot
clot
colitic
colt
colt
commit
commot
complot
coot
copilot
copt
coptic
coto
ictic
illicit
ilot
immit
implicit
impolitic
impot
itmo
licit
lilt
limit
loot
lotic
lotto
miliolitic
milt
mitotic
mitt
moit
molt
moot
motmot
mott
mott
motto
occipitootic
ocotillo
octic
octoic
octopi
omit
oolitic
optic
otic
otitic
otolitic
otomi
otto
otto
picot
pict
pict
pilot
pilot
pipit
pitpit
plot
politic
politico
polt
poot
potoo
pott
potto
ptotic
till
tillot
tilt
timo
tipiti
tiptilt
tiptop
titi
toco
toil
toit
toll
tolt
tomtit
tool
toom
toop
toot
topi
topic
topo
toto
#+end_example

** Scoring

These are the scoring rules for the game:

- Each 4 letter word is worth 1 point
- Longer words earn a point for each letter
- Pangram words that use all letters earn 7 extra points.

#+BEGIN_SRC raku :results output :tangle bee.raku :shebang "#!/usr/bin/env raku"
my @words = '/usr/share/dict/words'.IO.lines>>.lc;

sub MAIN(Str $chars, Str $centre, :$summary) {
    my $allowed = $chars.comb.Set;

    my $final-score = 0;
    my $found-words = 0;
    my $bonus-words = 0;

    for @words -> $word {
        my @chars = $word.comb;
        my $charset = @chars.Set;

        if +@chars >= 4 &&          # Is at least 4 chars
        $centre (elem) $charset &&  # Contains the center char (element of set)
        $charset (<=) $allowed {    # All chars are allowed (is a subset)

            my $word-score = +@chars == 4 ?? 1 !! +@chars;
            my $remainder = $allowed (-) $charset;
            unless +$remainder {
                $word-score += 7;
                $bonus-words++;
            }

            $final-score += $word-score;
            $found-words += 1;

            my $info = +$remainder ?? "unused {$remainder.keys}" !! 'pangram';
            sprintf("%15s = %2d    %s", $word, $word-score, $info).say unless $summary;
        }
    }

    say '';
    say "Found {$found-words} words from candidate characters {$chars.subst($centre, "[$centre]")}.";
    say "This spelling bee gets {$final-score} points, with {$bonus-words} bonus words.";
    say "Took " ~ (now - ENTER now).base(10, 2) ~ "s";
}
#+END_SRC

#+begin_src sh :results output
./bee.raku --summary mciplot t
#+end_src

#+RESULTS:
: 
: Found 93 words from candidate characters mciplo[t].
: This spelling bee gets 353 points, with 1 bonus words.
: Took 1.22s

#+begin_src sh :results output
./bee.raku --summary rdighlo r
#+end_src

#+RESULTS:
: 
: Found 53 words from candidate characters [r]dighlo.
: This spelling bee gets 158 points, with 1 bonus words.
: Took 1.23s

#+begin_src sh :results output
./bee.raku --summary yomahnr m
#+end_src

#+RESULTS:
: 
: Found 168 words from candidate characters yo[m]ahnr.
: This spelling bee gets 687 points, with 1 bonus words.
: Took 1.23s

#+begin_src sh :results output
./bee.raku --summary hunotgd u
#+end_src

#+RESULTS:
: 
: Found 72 words from candidate characters h[u]notgd.
: This spelling bee gets 308 points, with 2 bonus words.
: Took 1.25s

